buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.novoda:bintray-release:0.4.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    jcenter()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.parse:parse-android:1.13.1'
    compile 'com.parse:parsefacebookutils-v4-android:1.10.4@aar'

    compile 'com.parse.bolts:bolts-tasks:1.4.0'
    compile 'com.github.yongjhih.RxBolts:rxbolts2-tasks:2.0.2'

    compile 'io.reactivex.rxjava2:rxjava:2.0.8'

    compile 'com.facebook.android:facebook-android-sdk:4.18.0'
    //testCompile "org.mockito:mockito-core:1.+"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

apply plugin: 'com.novoda.bintray-release'

def getRepositoryUsername() {
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getRepositoryPassword() {
    return hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ""
}

publish {
    userOrg = 'yongjhih'
    groupId = 'com.infstory'
    artifactId = 'rxparse2-facebook-v4'
    publishVersion = '2.0.4'
    desc = 'Reactive ParseFacebooUtils v4'
    website = 'https://github.com/yongjhih/RxParse'
    bintrayUser = getRepositoryUsername()
    bintrayKey = getRepositoryPassword()
    dryRun = false
}

//group = 'com.github.yongjhih'

apply plugin: 'com.github.dcendents.android-maven'

apply from: 'javadoc.gradle'
